plugins {
    id 'com.android.library'
    id 'maven-publish'
}

// 检测是否在JitPack环境中构建
def isJitPackBuild = System.getenv("JITPACK") == "true" || 
                     gradle.startParameter.taskNames.toString().contains("publishToMavenLocal")

android {
    namespace 'com.orange.patchnative'
    compileSdk 36

    defaultConfig {
        minSdk 21
        targetSdk 36

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        // 只在非JitPack环境编译Native代码
        if (!isJitPackBuild) {
            externalNativeBuild {
                cmake {
                    cppFlags "-std=c++17 -fexceptions"
                    arguments "-DANDROID_STL=c++_shared"
                    // 支持多架构编译
                    abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
                }
            }

            ndk {
                // 支持的 ABI 架构
                abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // 只在非JitPack环境配置Native编译
    if (!isJitPackBuild) {
        externalNativeBuild {
            cmake {
                path "src/main/cpp/CMakeLists.txt"
                version "3.22.1"
            }
        }
    }

    // 在JitPack环境使用预编译的SO库
    sourceSets {
        main {
            if (isJitPackBuild) {
                jniLibs.srcDirs = ['prebuilt/jniLibs']
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.7.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.findByName("release")
                groupId = 'com.github.706412584'
                artifactId = 'patch-native'
                version = '1.2.2'
            }
        }
    }
}
